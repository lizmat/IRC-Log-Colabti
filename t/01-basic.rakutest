use Test;
use IRC::Log::Colabti;

my $filename = '2021-04-22';
my $date     = $filename.Date;
my $pos      = -1;

my $path     = $?FILE.IO.sibling: $filename;
my $log      = IRC::Log::Colabti.new: $path;
my @entries    = $log.entries;
my $nr-entries = +@entries;

sub test-common(
  $entry, $class, $hour, $minute, $ordinal, $nick, $control, $comment,
) {
    subtest $comment => {
        plan 13;

        isa-ok $entry, IRC::Log::Colabti::{$class};
        is $entry.date,     $date,    "is .date correct: $date";
        is $entry.hour,     $hour,    "is .hour correct: $hour";
        is $entry.minute,   $minute,  "is .minute correct: $minute";
        is $entry.ordinal,  $ordinal, "is .ordinal correct: $ordinal";
        is $entry.nick,     $nick,    "is .nick correct: $nick";
        is $entry.pos,      ++$pos,   "is .pos correct: $pos";

        is-deeply $entry.control, $control,
          "is .control ok: $control";
        is-deeply $entry.conversation, !$control,
          "is .conversation ok: $control.not()";

        is +$entry.entries, $nr-entries,
          'is nr-entries correct';
        is $entry.hhmm, sprintf('%02d%02d',$hour,$minute),
          'is .hhmm correct';
        is $entry.seen-at, sprintf('[%02d:%02d]',$hour,$minute),
          'is .seen-at correct';
        is $entry.target, (
          $ordinal
            ?? sprintf('%02s%02d-%d',$hour,$minute,$ordinal)
            !! sprintf('%02s%02d',$hour,$minute)
        ), 'is .target correct';
    }
}

plan 19 + @entries;

is-deeply IRC::Log::Colabti.IO2Date($path), $date,
  'was the date elided correctly';

isa-ok $log, IRC::Log::Colabti;
is-deeply $log.date, $date, 'is the date correct';
is +$log.problems, 0, 'did we see any problems';

my $entry = @entries.shift;
test-common $entry, 'Joined', 5, 36, 0, 'MasterDuke', True,
  'test joining';

$entry = @entries.shift;
test-common $entry, 'Message', 8, 1, 0, 'MasterDuke', False,
  'test message';
is $entry.text, 'any objections to merging ?',
  'is text ok in the message';

$entry = @entries.shift;
test-common $entry, 'Self-Reference', 8, 1, 1, 'sena_kun', False,
  'test message';
is $entry.text, 'still awaits the release blocker',
  'is text ok in the self-reference';

$entry = @entries.shift;
test-common $entry, 'Nick', 9, 35, 0, 'lizmmat_', True,
  'test nick change';
is $entry.new-nick, 'lizmat',
  'is new nick ok';

$entry = @entries.shift;
test-common $entry, 'Left', 11, 31, 0, 'nine', True,
  'test leaving';

$entry = @entries.shift;
test-common $entry, 'Mode', 22, 47, 0, 'ChanServ', True,
  'test mode';
is $entry.flags, '+o',
  'are the flags ok';
is-deeply $entry.nicks, Array[Str].new('tyil'),
  'are the nicks ok';

$entry = @entries.shift;
test-common $entry, 'Topic', 23, 48, 0, 'lizmat', True,
  'test kick';
is $entry.text, 'Perl Sucks!',
  'is the text ok';

$entry = @entries.shift;
test-common $entry, 'Kick', 23, 59, 0, 'tyil', True,
  'test kick';
is $entry.kickee, 'lizmat',
  'is the kickee ok';
is-deeply $entry.spec, '(lizmat))',
  'is the spec ok';

is +@entries, 0, 'did we see all the entries';

$log = IRC::Log::Colabti.new: $path.slurp, $date;
isa-ok $log, IRC::Log::Colabti;
is +$log.entries, $nr-entries, 'did we get the correct number of entries';
is-deeply $log.date, $date, 'is the date correct again';
is +$log.problems, 0, 'did we see any problems';

my %nicks := $log.nicks;
is %nicks.elems, 7, 'did we see 7 nicks';
subtest 'test all known nicks' => {
    plan 7;
    for <ChanServ lizmat lizmmat_ nine sena_kun tyil> -> $nick {
        is +%nicks{$nick}, 1, "did we see $nick once";
    }
    is +%nicks<MasterDuke>, 2, "did we see MasterDuke twice";
}

# vim: expandtab shiftwidth=4
