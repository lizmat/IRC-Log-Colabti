use Test;
use IRC::Log::Colabti;

my $filename = '2021-04-22';
my $date     = $filename.Date;
my $pos      = -1;

my $log      = IRC::Log::Colabti.new($?FILE.IO.sibling($filename));
my @entries    = $log.entries;
my $nr-entries = +@entries;

sub test-time-nick($entry, $class, $hour, $minute, $ordinal, $nick, $comment) {
    subtest $comment => {
        plan 10;

        isa-ok $entry, IRC::Log::Colabti::{$class};
        is $entry.date,     $date,    "is date correct: $date";
        is $entry.hour,     $hour,    "is hour correct: $hour";
        is $entry.minute,   $minute,  "is minute correct: $minute";
        is $entry.ordinal,  $ordinal, "is ordinal correct: $ordinal";
        is $entry.nick,     $nick,    "is nick correct: $nick";
        is $entry.pos,      ++$pos,   "is pos correct: $pos";

        is +$entry.entries, $nr-entries,
          'is nr-entries correct';
        is $entry.hhmm, sprintf('[%02s:%02d]',$hour,$minute),
          'is .hhmm correct';
        is $entry.target, (
          $ordinal
            ?? sprintf('%02s%02d-%d',$hour,$minute,$ordinal)
            !! sprintf('%02s%02d',$hour,$minute)
        ), 'is .target correct';
    }
}

plan 13 + @entries;

isa-ok $log, IRC::Log::Colabti;
is-deeply $log.date, $date, 'is the date correct';
is +$log.problems, 0, 'did we see any problems';

my $entry = @entries.shift;
test-time-nick $entry, 'Joined', 5, 36, 0, 'bisectable6',
  'test joining';

$entry = @entries.shift;
test-time-nick $entry, 'Message', 8, 1, 0, 'MasterDuke',
  'test message';
is $entry.text, 'any objections to merging ?',
  'is text ok in the message';

$entry = @entries.shift;
test-time-nick $entry, 'Self-Reference', 8, 1, 1, 'sena_kun',
  'test message';
is $entry.text, 'still awaits the release blocker',
  'is text ok in the self-reference';

$entry = @entries.shift;
test-time-nick $entry, 'Nick', 9, 35, 0, 'lizmmat_',
  'test nick change';
is $entry.new-nick, 'lizmat',
  'is new nick ok';

$entry = @entries.shift;
test-time-nick $entry, 'Left', 11, 31, 0, 'lizmat',
  'test leaving';

$entry = @entries.shift;
test-time-nick $entry, 'Mode', 22, 47, 0, 'ChanServ',
  'test mode';
is $entry.flags, '+o',
  'are the flags ok';
is-deeply $entry.nicks, Array[Str].new('lizmat'),
  'are the nicks ok';

is +@entries, 0, 'did we see all the entries';

$log = IRC::Log::Colabti.new(
  $?FILE.IO.sibling($filename).slurp,
  $filename.Date
);

isa-ok $log, IRC::Log::Colabti;
is +$log.entries, $nr-entries, 'did we get the correct number of entries';
is-deeply $log.date, $date, 'is the date correct again';
is +$log.problems, 0, 'did we see any problems';

# vim: expandtab shiftwidth=4
